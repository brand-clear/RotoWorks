version "5.0"
# ======================================================
# Measure the parallelism of a feature with respect to 
# a datum plane A.
# ------------------------------------------------------
#
# Parameters
# ----------
# $1 : double : Parallelism tolerance
# $2 : empty  : Validation container
# $3 : empty  : Measured parallelism container
# ------------------------------------------------------
#
# Returns
# -------
# $2 : int    : 1 if the parallelism is accepted, else 0
# $3 : double : Measured parallelism, if accepted
# ------------------------------------------------------
#
# Notes
# -----
# One and only one existing feature is assumed to be
# selected. A plane identified as datum A is assumed to 
# exist.
# ======================================================


DECLARE tolerance $1
DECLARE controlName
DECLARE measuredParallelism
DECLARE reprobe
DECLARE PASS 1
DECLARE FAIL 0
DECLARE errorStatus


# Set / get parallelism values
MEASURE CONTROL ADD ( "GD&T", "Parallelism", controlName )
MEASURE CONTROL TOLERANCE_ZONE LIMITS ( $controlName, $tolerance, , , , )
MEASURE CONTROL DATUM_FEATURES ( $controlName, "A", , , )
MEASURE CONTROL MEASURED GET ( $controlName, measuredParallelism )


# Check parallelism against the tolerance
IF $measuredParallelism > $tolerance
    MACRO INPUT QUESTION ( reprobe, {"The measured parallelism (${measuredParallelism}) exceeds the allowable tolerance of ${tolerance}.", "Do you want to reprobe?"} )
    
    IF $reprobe == 1
        # Remove parallelism control and return a FAIL
        MEASURE CONTROL REMOVE ( $controlName )
        MACRO OUTPUT_ARGUMENT ( 2, $FAIL )
        MACRO END ( "No Error" )
    ENDIF
    
ENDIF    


# User has accepted the deviation, return PASS     
MACRO OUTPUT_ARGUMENT ( 2, $PASS )
MACRO OUTPUT_ARGUMENT ( 3, $measuredParallelism )

